plugins {
    id 'java'
}

group = 'com.firstTrip'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.microsoft.playwright:playwright:1.46.0'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'org.slf4j:slf4j-simple:2.0.9'
    implementation 'org.projectlombok:lombok:1.18.34'

    testImplementation 'org.testng:testng:7.10.2'
    testImplementation 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
}

test {
    useTestNG() {
        if (project.hasProperty('testng.suites')) {
            project.property('testng.suites').toString().split(',').each { suite ->
                System.out.println('Running suite: ' + suite)
                suites 'src/test/resources/' + suite + '-suite.xml'
            }
        }

        if (project.hasProperty('testng.parallel')) {
            parallel project.property('testng.parallel') as String
        }

        if (project.hasProperty('testng.threadCount')) {
            threadCount project.property('testng.threadCount') as int
        }

        if (project.hasProperty('testng.groups')) {
            includeGroups project.property('testng.groups').toString().split(',')
        }
    }

    if (project.hasProperty('credentials')) {
        systemProperty('external.credentials.file', project.property('credentials'))
    }
}

clean {
    delete '.auth'
}